buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath "kz.greetgo.plugins:greetgo.gradle.plugins:0.0.5"
  }
}

plugins {
  id "org.ajoberstar.grgit" version "2.3.0"
}

//noinspection UnnecessaryQualifiedReference
apply plugin: kz.greetgo.gradle.plugins.GgRepoUploadPlugin
//noinspection UnnecessaryQualifiedReference
apply plugin: kz.greetgo.gradle.plugins.MavenUploadPlugin

def ggRepo = System.getenv('GG_REPO')

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

version = '2.0.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "kz.greetgo"

repositories {
  mavenLocal()
  if (ggRepo?.trim()) maven { url ggRepo.trim() }
  mavenCentral()
}

dependencies {
  ext.greetgoUtilVersion = '0.1.1'

  compile "kz.greetgo.util:greetgo.util:$greetgoUtilVersion"

  testCompile "kz.greetgo.depinject:greetgo.depinject:2.0.3"

  testCompile 'org.testng:testng:6.5.1'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'
  testCompile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'

}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('encoding', 'UTF-8')
  options.addStringOption('charSet', 'UTF-8')
}

test.useTestNG()

//test { options.suites("test1.xml") }

idea.module.inheritOutputDirs = false
idea.module.outputDir = file("$buildDir/classes/main/")
idea.module.testOutputDir = file("$buildDir/classes/test/")

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

sourceSets.main.java.srcDirs = ["src"]
sourceSets.test.java.srcDirs = ["test_src"]
sourceSets.main.resources.srcDirs = ["src_resources"]
sourceSets.test.resources.srcDirs = ["test_resources"]

sourceSets.main.resources {
  srcDirs += sourceSets.main.java.srcDirs
  exclude '**/*.java'
}
sourceSets.test.resources {
  srcDirs += sourceSets.test.java.srcDirs
  exclude '**/*.java'
}

jar {
  manifest {
    attributes 'Specification-Title': project.name
    attributes 'Specification-Version': version
    attributes 'Specification-Vendor': "greetgo!"
    attributes 'Implementation-Version': grgit ? grgit.head().id : "unknown"
    attributes 'Created-By': System.getProperty("user.name")
    attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
  }
}

uploadToMavenCentral {
  description = 'greetgo compiler simple interface'
  url = 'https://github.com/greetgo/greetgo.java_compiler'
  scm {
    url = 'https://github.com/greetgo/greetgo.java_compiler.git'
  }
  developer {
    id = 'pompei'
    name = 'Evgenij Kolpakov'
    email = 'ekolpakov@greet-go.com'
  }
}
